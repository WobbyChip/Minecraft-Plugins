plugins {
    id "java"
    id "io.papermc.paperweight.userdev" version "1.5.9"
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17
group = project.group
defaultTasks "rebuild"

compileJava {
    options.encoding = "UTF-8"
    options.compilerArgs += ["-parameters"]
}

repositories {
    mavenCentral()
    mavenLocal()
    maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
    maven { url "https://repo.papermc.io/repository/maven-public/" }
    maven { url "https://repo.codemc.io/repository/nms/" }
    maven { url "https://repo.dmulloy2.net/repository/public/" }
    maven { url "https://oss.sonatype.org/content/groups/public/" }
    maven { url "https://repo.essentialsx.net/releases/" }
}

dependencies {
    compileOnly "com.comphenix.protocol:ProtocolLib:5.1.0"
    compileOnly "net.essentialsx:EssentialsX:2.20.0"
    compileOnly "io.papermc.paper:paper-api:${project.minecraft_version}-R0.1-SNAPSHOT"
    paperweight.paperDevBundle("${project.minecraft_version}-R0.1-SNAPSHOT")
}

processResources {
    filesMatching('**/plugin.yml') {
        filter { line ->
            line.replaceAll("version: 1.0", "version: ${project.minecraft_version}")
        }
    }
}

clean {
    delete rootProject.buildDir
}

task rebuild {
    dependsOn clean
    finalizedBy build
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
        }
        resources {
            srcDirs = ['src/main/java', 'src/main/resources']
            exclude '**/*.java'
        }
    }
}

tasks {
    assemble {
        dependsOn(reobfJar)
    }

    compileJava {
        options.release.set(17)
    }

    reobfJar {
      outputJar.set(layout.buildDirectory.file("SMPTweaks.jar"))
    }
}