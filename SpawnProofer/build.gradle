plugins {
	id "fabric-loom" version "1.6-SNAPSHOT"
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17
version = project.mod_version
group = project.maven_group
defaultTasks "rebuild"

repositories {
	maven { url "https://masa.dy.fi/maven" }
	maven { url "https://maven.shedaniel.me/" }
	maven { url "https://maven.terraformersmc.com/releases/" }
    mavenCentral()
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	modImplementation "com.terraformersmc:modmenu:${project.modmenu_version}"
	modApi "com.terraformersmc:modmenu:${project.mod_api}"
	modApi "me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config}"
	implementation "com.google.code.gson:gson:2.10.1"
}

processResources {
    filesMatching("**/fabric.mod.json") {
        filter { line ->
            line.replaceAll("\"version\": \"1.0\",", "\"version\": \"${project.minecraft_version}\",")
        }
    }
}

tasks.withType(JavaCompile).configureEach {
	it.options.encoding = "UTF-8"
	it.options.release = 17
}

clean {
    delete "run"
    delete "build"
}

task rebuild {
    dependsOn "clean"
    dependsOn "build"
    tasks.findByName("build").mustRunAfter "clean"

    doLast {
        copy {
            from "build/libs/${project.mod_name}-${project.mod_version}.jar"
            into "build"
        }
    }
}

jar {
	from("LICENSE") { rename { "${it}"} }
}
